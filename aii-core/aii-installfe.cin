#!@PERL_EXECUTABLE@ -w
################################################################################
# This is '@SELF@', a @NAME@'s file
################################################################################
#
# VERSION:    @VERSION@, @DATE@
# AUTHOR:     @AUTHOR@
# MAINTAINER: @MAINTAINER@
# LICENSE:    @LICENSE@
#
################################################################################
# Coding style: emulate <TAB> characters with 4 spaces, thanks!
################################################################################
#
# @SELF@      AII Remote Management Shell
#
################################################################################

=pod

=head1 NAME

aii-installfe - AII remote command line management frontend

=head1 SYNOPSIS

 aii-installfe [options] <--boot <hostname|regexp>      |
                          --bootlist <filename>         |
                          --configure <hostname|regexp> |
                          --configurelist <filename>    |
                          --install <hostname|regexp>   |
                          --installlist <filename>      |
                          --remove <hostname|regexp>    |
                          --removelist <filename>       |
                          --removeall                   |
                          --status <hostname|regexp>    |
                          --statuslist <filename> 

=head1 DESCRIPTION

The aii-installfe utility provides a command line interface or
frontend to remotely manage the AII tools. With aii-installfe we can
select if a client node has to be installed or not, to add/update the
AII specific configuration of nodes and to remove nodes from the AII system.
aii-installfe receives as input from the user a lists of nodes
and their installation status (to be installed/booted from local disk).
Regular expressions can be used to specify hostnames
(for example node00[1-9] or node.*). aii-installfe executes the frontend
aii-shellfe on all the specified installation servers.
Servers can be listed in a configuration file (see below).

aii-installfe is part of the AII system of the quattor tool suite
(see http://www.quattor.org for more information).

=head1 COMMANDS

=over 4

=item --boot <hostname|regexp>

Select the boot from local disk for <hostname>. Perl regular
expressions can be used.

=item --bootlist <filename>

Select boot from local disk for hosts listed on <filename>. Hosts have to
be listed one per line. Lines starting with # are comment.

=item --configure <hostname|regexp>

Configure <hostname>. Perl regular expressions can be used.

=item --configurelist <filename>

Configure hosts listed on <filename>. Hosts have to
be listed one per line. Lines starting with # are comment.

=item --install <hostname|regexp>

Select the installation for <hostname>. Perl regular expressions
can be used.

=item --installlist <filename>

Select installation for the hosts listed on <filename>. Hosts have to
be listed one per line. Lines starting with # are comment.

=item --remove <hostname|regexp>

Remove the configuration for <hostname>. Perl regular expressions
can be used.

=item --removelist <filename>

Remove configurations for hosts listed on <filename>. Hosts have to
be listed one per line. Lines starting with # are comment.

=item --removeall

Remove configurations for *ALL* hosts configured. Useful only in case
of problems/test.

=item --status <hostname|regexp>

Report the boot status (boot from local disk/install) for <hostname> or
for all hostnames that match the regular expression <regexp>

=item --statuslist <filename>

Report the boot status (boot from local disk/install) for hosts listed
on <filename>. Hosts have to be listed one per line. Lines starting
with # are comment.

=back

=head1 OPTIONS

=over 4

=item --servers <user1@server1 user2@server2 ...>

Installation servers to be updated remotely via ssh. Use'@'
to select the remote user to use. E.g:

 servers = john@install-1.my_domain.org

If there are more servers, use ' ' to separate them:

 servers = john@install-1.my_domain.org john@install-2.my_domain.org

If you are running *all* tools (aii-dhcp, aii-nbp, aii-osinstall)
on *this* machine, use just:

 server = localhost

They will be executed *directly* (no ssh required). Of course you can mix:

 servers = localhost john@install-1.asdf.fi

=item --nodhcp

Run do not update DHCP configuration.

=item --nonbp

Do not update NBP configuration.

=item --noosinstall

Do not update OS installer configurations.

=back

=head2 Other Options

=over

=item --help

Displays a help message with all options and default settings.

=item --version

Displays program version information.

=item --verbose

Print verbose details on operations.

=item --debug <1..5>

Set the debugging level to <1..5>.

=item --cfgfile <path>

Use as configuration file <path> instead of the default
configuration file @QTTR_ETC@/aii/aii-nbp.conf.

=item --logfile <file>

Store and append log messages in <file>.

=back

=head1 CONFIGURATION FILE

=over 4

Default values of command lines options can be specified in the file
@QTTR_ETC@/aii/aii-installfe.conf using syntax:

 <option> = <value>

=back

=head1 AUTHORS

Enrico Ferro <enrico.ferro@pd.infn.it>

Rafael Garcia Leiva <angel.leiva@uam.es>

=cut

#
# Standard Common Application Framework beginning sequence
#

#
# Beginning sequence for EDG initialization
#
BEGIN {

    # use perl libs in @QTTR_PERLLIB@
    unshift(@INC, '@QTTR_PERLLIB@');
    unshift(@INC,'/opt/edg/lib/perl');

}

#------------------------------------------------------------
# Application
#------------------------------------------------------------

package aii_installfe;

use CAF::Application;
use CAF::Reporter;
use LC::Exception qw (SUCCESS throw_error);

use strict;

our @ISA= qw(CAF::Application CAF::Reporter);

#
# Public Methods/Functions for CAF
#
sub app_options {

    push(my @array,

        { NAME    => 'configure=s',
          HELP    => 'Node(s) to be configured (can be a regexp)',
          DEFAULT => undef },

        { NAME    => 'configurelist=s',
          HELP    => 'File with the nodes to be configured',
          DEFAULT => undef },

        { NAME    => 'remove=s',
          HELP    => 'Node(s) to be removed (can be a regexp)',
          DEFAULT => undef },

        { NAME    => 'removelist=s',
          HELP    => 'File with the nodes to be removed',
          DEFAULT => undef },

        { NAME    => 'removeall',
          HELP    => 'Remove ALL nodes configured',
          DEFAULT => undef },

        { NAME    => 'boot=s',
          HELP    => 'Node(s) to boot from local disk (can be a regexp)',
          DEFAULT => undef },

        { NAME    => 'bootlist=s',
          HELP    => 'File with the nodes to boot from local disk',
          DEFAULT => undef },

        { NAME    => 'install=s',
          HELP    => 'Nodes(s) to be installed (can be regexp)',
          DEFAULT => undef },

        { NAME    => 'installlist=s',
          HELP    => 'File with the nodes to be installed',
          DEFAULT => undef },

        { NAME    => 'status=s',
          HELP    => 'Report current boot/install status for the node ' .
                     '(can be a regexp)',
          DEFAULT => undef },

        { NAME    => 'statuslist=s',
          HELP    => 'File with the nodes to report boot/install status',
          DEFAULT => undef },

        { NAME    => 'nodhcp',
          HELP    => 'Do not update DHCP server(s)',
          DEFAULT => undef },

        { NAME    => 'nonbp',
          HELP    => 'Do not update NBP configuration',
          DEFAULT => undef },

        { NAME    => 'noosinstall',
          HELP    => 'Do not update OS installer configuration',
          DEFAULT => undef },

        { NAME    => 'servers=s',
          HELP    => 'Installation servers to be udpated',
          DEFAULT => 'localhost' },

        # other common options

        { NAME    => 'logfile=s',
          HELP    => 'path/filename to use for aii-installfe logs',
          DEFAULT => "@QTTR_LOG@/aii-installfe.log" },

        { NAME    => 'cfgfile=s',
          HELP    => 'configuration file for aii-shellfe defaults',
          DEFAULT => '@QTTR_ETC@/aii/aii-installfe.conf' },

        { NAME    => 'noaction',
          HELP    => 'do not actually perform operations',
          DEFAULT => undef }

        # options inherited from CAF
        #   --help
        #   --version
        #   --verbose
        #   --debug
        #   --quiet

        );

    return(\@array);

}

sub _initialize {

    my $self = shift;

    #
    # define application specific data.
    #

    # external version number
    $self->{'VERSION'} = '@VERSION@';

    # show setup text
    $self->{'USAGE'} = "Usage: aii-installfe [options]\n";

    #
    # log file policies
    #

    # append to logfile, do not truncate
    $self->{'LOG_APPEND'} = 1;

    # add time stamp before every entry in log
    $self->{'LOG_TSTAMP'} = 1;

    #
    # start initialization of CAF::Application
    #
    unless ($self->SUPER::_initialize(@_)) {
        return(undef);
    }

    # start using log file (could be done later on instead)
    $self->set_report_logfile($self->{'LOG'});

    return(SUCCESS);

}

#############################################################
# Main Program
#############################################################

package main;

use LC::Exception qw (SUCCESS throw_error);
use File::Basename;

use strict;
use vars qw($this_app %SIG);

#
# Global Attributes:
#

my ($Options, $Files);
my (@Servers);

$Options = "";
$Files   = "";
@Servers = ();

#
# run_remotely($user_at_host)
#
# run aii-shellfe in a remote AII server
#
sub run_remotely ($) {

    my $user_at_host = shift;
    my ($user, $host);

    ($user, $host) = ('', '');
    ($user, $host) = split(/\@/, $user_at_host);

    $this_app->debug(3, "aii-installfe: user: $user at host: $host");

    if ($user eq '' || $host eq '') {
        $this_app->error("aii-installfe: bad remote user/server "
                         . "specified ($user_at_host)");
        return(1);
    }

    $host = (gethostbyname($host))[0];

    # remote copy files with node lists to AII server
    if ($Files ne "") {
        if ($this_app->option('noaction')) {
            $this_app->info("aii-installfe: run: "
                             . "\"scp $Files $user_at_host:\"");
        } else {
            $this_app->debug(2, "aii-installfe: copy node lists: "
                                . "\"scp $Files $user_at_host:\"");
            if (system("scp $Files $user_at_host:")) {
                $this_app->error("installfe: failed execution of "
                                 . "\"scp $Files $user_at_host:\"");
                return(1);
            }
        }
    }

    # remote run aii-shellfe on AII server
    if ($this_app->option('noaction')) {
        $this_app->info("aii-installfe: run: "
                        . "\"ssh $user_at_host /usr/bin/sudo "
                        . "@QTTR_SBIN@/aii-shellfe $Options\"");
    } else {
        $this_app->debug(2, "aii-installfe: run aii-shellfe remotely: "
                            . "\"ssh $user_at_host /usr/bin/sudo "
                            . "@QTTR_SBIN@/aii-shellfe $Options\"");
        if (system("ssh $user_at_host /usr/bin/sudo "
                   . "@QTTR_SBIN@/aii-shellfe $Options 2>&1")) {
            $this_app->error("installfe: failed execution of "
                        . "\"ssh $user_at_host /usr/bin/sudo "
                        . "@QTTR_SBIN@/aii-shellfe $Options\"");
            return(1);
        }
    }

    return(0);

}

#
# run_localy()
#
# run aii-shellfe on the local host
#
sub run_locally {

    if ($this_app->option('noaction')) {
        $this_app->info("aii-installfe: run localy: ".
                        "@QTTR_SBIN@/aii-shellfe $Options");
    } else {
        $this_app->debug(2, "aii-installfe: running localy: "
                            . "@QTTR_SBIN@/aii-shellfe $Options");
        if (system("@QTTR_SBIN@/aii-shellfe $Options 2>&1")) {
            $this_app->error("aii-installfe: failed execution of "
                             . "@QTTR_SBIN@/aii-shellfe $Options");
            return(1);
        }
    }

    return(0);

}

#
# parse_options()
#
# retrieve options for shellfe
#
sub parse_options {

    my $item;

    #
    # retrieve options for shellfe
    #

    $Options = '';

    $Options .= ' --nodhcp'      if $this_app->option('nodhcp');
    $Options .= ' --nonbp'       if $this_app->option('nonbp');
    $Options .= ' --noosinstall' if $this_app->option('noosinstall');

    $Options .= ' --verbose'     if $this_app->option('verbose');
    $Options .= ' --quiet'       if $this_app->option('quiet');

    if ($this_app->option('debug')) {
        $Options .= ' --debug ' . $this_app->option('debug');
    }

    if ($this_app->option('removeall'))   {
        $Options .= ' --removeall';
        $this_app->warn('aii-installfe: remove all option enabled');
    } else {

        for $item ('install', 'boot', 'configure', 'remove', 'status') {

            if ($this_app->option($item)) {
                $Options .= " --$item '" . $this_app->option($item) . "'";
            }

            if ($this_app->option($item . 'list')) {
                if (! -f $this_app->option($item . 'list')) {
                    $this_app->error('aii-installfe: file not found ('
                               . $this_app->option($item . 'list') . ')');
                    return(1);
                }
                $Options .= ' --' . $item . 'list ' .
                            basename($this_app->option($item . 'list'));
                $Files .= ' ' . $this_app->option($item . 'list');
            }


        }

    }

    return(0);

}

#------------------------------------------------------------
# main loop
#------------------------------------------------------------

# fix umask
umask (022);

# unbuffer STDOUT & STDERR
autoflush STDOUT 1;
autoflush STDERR 1;

my $server;

#
# initialize the main class.
#
unless ($this_app = aii_installfe->new($0, @ARGV)) {
    throw_error('aii-installfe: cannot start application');
}

# process command line options
$this_app->debug(1, "aii-installfe: reading cmd line");
if (parse_options()) {
    $this_app->error("aii-installfe: failed to process cmd line ");
    exit(1);
}
if ($Options eq '') {
    $this_app->warn('aii-installfe: no actions specified');
    exit(0);
}


#
# run tools locally (without ssh) or remotely (via ssh)
#

@Servers = split ('\s+', $this_app->option('servers'));

$this_app->verbose("aii-installfe: servers = " . @Servers);

# Pass cmdline data to the node
foreach $server (@Servers) {

    $this_app->debug(1, "aii-installfe: server: $server");

    if ($server eq 'localhost') {
        if (run_locally()) {
            $this_app->error("aii-installfe: failed to run "
                             . "aii-shellfe locally");
            exit(1);
        }
    } else {
        if (run_remotely($server)) {
            $this_app->error("aii-installfe: failed to run "
                             . "aii-shellfe remotely");
            exit(1);
        }
    }

}

exit (0);
